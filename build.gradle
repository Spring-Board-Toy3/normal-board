buildscript {
    ext {
        springBootVersion = '2.7.13'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.15.RELEASE"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'

    group = 'com'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'com.mysql:mysql-connector-j'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':module-admin') {
    dependencies {
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        // JSP
        implementation 'javax.servlet:jstl'
        implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
        // MyBatis
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.3.1'
        testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.3.1'
    }
}

project(':module-board') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'

        testImplementation 'org.springframework.security:spring-security-test'
    }
}


